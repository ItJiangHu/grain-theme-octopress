apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
version = '1.0'
defaultTasks 'grain'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

clean.doLast { ant.delete(dir: ".cache"); ant.delete(dir: "target") }

buildscript {
    project.ext {
        grainProps = new Properties()
        grainProps.load(new FileInputStream("$project.projectDir/application.properties"))
        grainVersion = grainProps.getProperty('grain.version')

        if (!project.grainVersion) {
            throw new RuntimeException('Grain version is not set in properties file')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'http://sysgears.github.io/grain/repo/'
    }
}

dependencies {
    compile "com.sysgears.grain:grain:$project.grainVersion"

    compile 'org.codehaus.groovy:groovy:2.1.2'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['theme/src']
        }
    }
}

project.ext {
    mainClass = 'com.sysgears.grain.Main'
    classpath = sourceSets.main.runtimeClasspath
}

def getGrainArgs(project) {
    project.hasProperty('args') && project.args.trim() ?
        project.args.split(',').toList() : []
}

task grain() {
    project.javaexec {
            classpath = project.classpath
            main = project.mainClass
            args = getGrainArgs(project)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

idea {
    module {
        excludeDirs = [".cache", ".idea", ".gradle", ".nb-gradle", ".settings", "bin", "out", "target"].collect { file(it) }
    }
}
