apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
version = '0.3.0'
defaultTasks 'grain'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

clean.doLast { ant.delete(dir: '.cache'); ant.delete(dir: 'target') }

buildscript {
    project.ext {
        grainProps = new Properties()
        grainProps.load(new FileInputStream("$project.projectDir/application.properties"))
        grainVersion = grainProps.getProperty('grain.version')

        if (!project.grainVersion) {
            throw new RuntimeException('Grain version is not set in properties file')
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.sysgears.com/releases/'
    }
    maven {
        url 'http://repo.sysgears.com/snapshots/'
    }
}

dependencies {
    compile "com.sysgears.grain:grain:$project.grainVersion"

    compile 'org.codehaus.groovy:groovy:2.1.2'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['theme/src']
        }
    }
}

project.ext {
    mainClass = 'com.sysgears.grain.Main'
    classpath = sourceSets.main.runtimeClasspath
}

def getGrainJvmArgs() {
    (System.getenv('GRAIN_OPTS') ?:
        '-server -Xmx512M -Xms64M -XX:PermSize=32m -XX:MaxPermSize=256m ' +
        '-Dfile.encoding=UTF-8 -XX:+CMSClassUnloadingEnabled ' +
        '-XX:+UseConcMarkSweepGC').trim().split(' ').toList()
}

def getGrainArgs(project) {
    project.hasProperty('args') && project.args.trim() ?
        project.args.split(',').toList() : []
}

task grain {
    doLast {
        project.javaexec {
                classpath = project.classpath
                main = project.mainClass
                args = getGrainArgs(project)
                jvmArgs = getGrainJvmArgs()
                standardInput = System.in
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

idea {
    module {
        excludeDirs = ['.cache', '.idea', '.gradle', '.nb-gradle', '.settings', 'bin', 'out', 'target', 'gradle'].collect { file(it) }
    }
}
